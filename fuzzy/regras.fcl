FUNCTION_BLOCK regras	// Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT				
	natalidade : REAL;
        mortalidade : REAL;
        migracao : REAL;
END_VAR

// Define output variable
VAR_OUTPUT				
	crescimento : REAL;
END_VAR

// Fuzzify input variable 'natalidade': {'baixa', 'media' , 'alta'}
FUZZIFY natalidade			
	TERM baixa := (0,1) (15.41,0);
	TERM media := (15.12, 0) (15.88, 1) (16.06, 1) (17.92, 0);
	TERM alta := (16.76,0) (24, 1);
END_FUZZIFY

// Fuzzify input variable 'mortalidade': {'baixa', 'media' , 'alta'}
FUZZIFY mortalidade			
	TERM baixa := (0,1) (5.82,0);
	TERM media := (5.53, 0) (5.91, 1) (6.12, 1) (7.01, 0) ;
	TERM alta := (6.63,0) (9,1);
END_FUZZIFY

// Fuzzify input variable 'migracao': {'baixa', 'media' , 'alta'}
FUZZIFY migracao			
	TERM baixa := (-1,1) (0.25,0);
	TERM media := (0.23, 0) (0.29, 1) (0.39, 1) (0.53, 0);
	TERM alta := (0.43,0) (1,1);
END_FUZZIFY

// Defzzzify output variable 'crescimento' : {'baixo', 'alto', 'medio', muito_alto}
DEFUZZIFY crescimento			
	TERM baixo := (0.3,1) (0.55,1) (0.9, 0);
	TERM medio := trian 0.5 0.88 1.3 ;
	TERM alto := (1.2,0) (1.5, 1) (1.6, 0);
        TERM muito_alto := (1.4, 0) (2.5, 1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

        RULE 1 : IF natalidade IS baixa AND mortalidade IS baixa THEN crescimento IS medio;
        RULE 2 : IF natalidade IS alta AND mortalidade IS baixa THEN crescimento IS muito_alto;
        RULE 3 : IF natalidade IS baixa AND mortalidade IS alta THEN crescimento IS baixo;
        RULE 4 : IF natalidade IS alta AND mortalidade IS alta THEN crescimento IS medio;
        RULE 5 : IF natalidade IS media AND mortalidade IS baixa THEN crescimento IS medio;
        RULE 6 : IF natalidade IS baixa AND mortalidade IS media THEN crescimento IS baixo;
        RULE 7 : IF natalidade IS media AND mortalidade IS alta THEN crescimento IS baixo;
        RULE 8 : IF natalidade IS alta AND mortalidade IS media THEN crescimento IS alto;
        RULE 9 : IF natalidade IS media AND mortalidade IS media THEN crescimento IS alto;
        RULE 10 : IF migracao IS baixa THEN crescimento IS baixo;
        RULE 11 : IF migracao IS media THEN crescimento IS medio;
        RULE 12 : IF migracao IS alta THEN crescimento IS alto;

END_RULEBLOCK

END_FUNCTION_BLOCK


